apply plugin: "java"
apply plugin: "checkstyle"
apply plugin: "eclipse"
// apply plugin: "findbugs"

// forces javac 1.7 
// this could change but we should all use the same one
sourceCompatibility = 1.7

// compile with debugging symbols
compileJava.options.debugOptions.debugLevel = "source,lines,vars"

sourceSets {
    main {
        resources {
            srcDir 'jme3 /assets'
        }
    }
}

repositories {
  mavenCentral()
}

dependencies {
  testCompile 'junit:junit:4.+'
  testCompile 'org.jmock:jmock-junit4:2.6.0'
  compile 'net.sf.jung:jung-3d:2.0.1'
  compile 'net.sf.jung:jung-graph-impl:2.0.1'
  compile fileTree("lib")
}

jar {
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  manifest {
    attributes 'Main-Class': 'org.visualheap.app.VisualHeap'
  }
}

task locals(type: Jar) {
  baseName = "locals"
  from(sourceSets.main.output) {
      include "org/group/jdiTest/locals/**" 
  }
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

  manifest.attributes("Main-Class": "org.group.jdiTest.locals.Locals")
}

task breakpoints(type: Jar) {
  baseName = "breakpoints"
  from(sourceSets.main.output) {
      include "org/group/jdiTest/breakpoints/**" 
  }
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

  manifest.attributes("Main-Class": "org.group.jdiTest.breakpoints.Breakpoints")
}

task game(type: Jar) {
  baseName = "game"
  from(sourceSets.main.output) {
      include "org/visualheap/**"
  }
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  from { fileTree "jme3 /assets" }

  manifest.attributes("Main-Class": "org.visualheap.app.Game")
}

checkstyle {
  configFile = new File("config/checkstyle/sun_checks.xml")
  showViolations = false
  ignoreFailures = true
  sourceSets {
    main { 
      java {
        exclude "**/jdiTest/**"
      }
    }
  }
}

checkstyleMain << {
  ant.xslt(
      in: "$buildDir/reports/checkstyle/main.xml",
      style:"config/checkstyle/checkstyle-noframes.xsl",
      out:"$buildDir/reports/checkstyle/checkstyle.html"
  )
}

// findbugs {
//   ignoreFailures = true
// }
// 
// tasks.withType(FindBugs) {
//   reports {
//     xml.enabled = false
//     html.enabled = true
//   }
// }
